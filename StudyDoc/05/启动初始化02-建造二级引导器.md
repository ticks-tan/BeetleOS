# 建造二级引导器

操作系统文件都是存放在硬盘中且具有一定格式，在启动计算机之前需要先建造二级引导器，用于解析内核文件，收集机器信息，确保我们的电脑满足操作系统运行条件。

二级引导器收集的信息存放在内存中，我们需要设计一个数据结构用于存放这些信息，并将这个结构放置到内存 `1MB` 的地方，方便以后我们操作系统访问。需要注意刚加电计算机会先运行 BIOS 程序，BIOS后面加载GRUB程序，GRUB程序将CPU工作模式切换到32位保护模式，所以可以访问4GB内存空间，不是在实模式下运行的。

为了完成引导，需要规划出不同的功能模块，下面是主要的功能模块：

| 文件 | 功能                                    |
| ---- | --------------------------------------- |
|      | GRUB头汇编（ASM）                       |
|      | GRUB头，将二级引导器放入指定内存（C++） |
|      | 实现BIOS中断（ASM）                     |
|      | 二级引导器核心入口（ASM）               |
|      | 二级引导器核心入口（C++）               |
|      | 收集机器信息并建立页面数据（C++）       |
|      | 检查CPU和内存视图（C++）                |
|      | 解析操作系统映像文件（C++）             |
|      | 切换显卡图形模式（C++）                 |
|      | 实现字符串输出（C++）                   |

## GRUB头部分

GRUB头部分由两个文件组成，一个汇编、一个C++文件，汇编文件有固定的GRUB头格式信息，GRUB识别执行，汇编之后会初始化C++工作环境，调用C++函数，C++函数解析出二级引导器文件并放到指定内存中。

汇编文件主要功能是初始化CPU寄存器、加载GDT、切换到保护模式。

首先是GRUB需要的固定格式信息：

```asm

MBT_HDR_FLAGS  EQU 0x00010003
MBT_HDR_MAGIC  EQU 0x1BADB002
MBT2_MAGIC  EQU 0xe85250d6
global _start
extern inithead_entry
[section .text]
[bits 32]
_start:
  jmp _entry
align 4
mbt_hdr:
  dd MBT_HDR_MAGIC
  dd MBT_HDR_FLAGS
  dd -(MBT_HDR_MAGIC+MBT_HDR_FLAGS)
  dd mbt_hdr
  dd _start
  dd 0
  dd 0
  dd _entry
ALIGN 8
mbhdr:
  DD  0xE85250D6
  DD  0
  DD  mhdrend - mbhdr
  DD  -(0xE85250D6 + 0 + (mhdrend - mbhdr))
  DW  2, 0
  DD  24
  DD  mbhdr
  DD  _start
  DD  0
  DD  0
  DW  3, 0
  DD  12
  DD  _entry 
  DD  0  
  DW  0, 0
  DD  8
mhdrend:
```

之后关闭中断，加载GDT，进入保护模式：

```asm
_entry:
  cli           ; 关中断
  in al, 0x70   ; in 从端口读取
  or al, 0x80   ; out 写入端口
  out 0x70,al  ; 关掉不可屏蔽中断   
  lgdt [GDT_PTR] ; 加载GDT地址到GDTR寄存器
  jmp dword 0x8 :_32bits_mode ; 长跳转刷新CS影子寄存器
  ;………………
;GDT全局段描述符表
GDT_START:
knull_dsc: dq 0
kcode_dsc: dq 0x00cf9e000000ffff
kdata_dsc: dq 0x00cf92000000ffff
k16cd_dsc: dq 0x00009e000000ffff ; 16位代码段描述符
k16da_dsc: dq 0x000092000000ffff ; 16位数据段描述符
GDT_END:
GDT_PTR:
GDTLEN  dw GDT_END-GDT_START-1  ; GDT界限
GDTBASE  dd GDT_START
```

之后初始化段寄存器、通用寄存器、栈等，调用C++函数：

```asm
_32bits_mode：
  mov ax, 0x10
  mov ds, ax
  mov ss, ax
  mov es, ax
  mov fs, ax
  mov gs, ax
  xor eax,eax
  xor ebx,ebx
  xor ecx,ecx
  xor edx,edx
  xor edi,edi
  xor esi,esi
  xor ebp,ebp
  xor esp,esp
  mov esp,0x7c00 ; 设置栈顶为0x7c00
  call inithead_entry ; 调用inithead_entry（函数在C++中实现）
  jmp 0x200000  ; 跳转到0x200000地址，即二级引导器开始位置执行
```

C++文件中函数功能主要就是查找剩余两个模块文件并将其加载到指定内存区域，方便之后运行。写入指定内存之后便由上面汇编跳转到二级引导器入口位置了。

## 进入二级引导器

由于代码模块改变（参见：[Linux0.11启动](https://mp.weixin.qq.com/s?__biz=Mzk0MjE3NDE0Ng==&mid=2247499734&idx=1&sn=89045b51e41d2e74754f5475d25d7b54&chksm=c2c5857bf5b20c6d1698df2abd0e1eea601e83612ee6af58dc8eb79f93c751f73d7c62e565ce&scene=21#wechat_redirect) ），对应代码段、数据段基址变化，需要重新初始化GDT、IDT、寄存器等，之后调用二级引导器C++函数正式进入二级引导器，这里就需要重新写一段汇编。

```asm

_entry:
  cli
  lgdt [GDT_PTR] ; 加载GDT地址到GDTR寄存器
  lidt [IDT_PTR] ; 加载IDT地址到IDTR寄存器
  jmp dword 0x8 :_32bits_mode ; 长跳转刷新CS影子寄存器
_32bits_mode:
  mov ax, 0x10  ; 数据段选择子(目的)
  mov ds, ax
  mov ss, ax
  mov es, ax
  mov fs, ax
  mov gs, ax
  xor eax,eax
  xor ebx,ebx
  xor ecx,ecx
  xor edx,edx
  xor edi,edi
  xor esi,esi
  xor ebp,ebp
  xor esp,esp
  mov esp,0x90000 ; 使得栈底指向了0x90000
  call ldrkrl_entry ; 调用ldrkrl_entry函数
  xor ebx,ebx
  jmp 0x2000000 ; 跳转到0x2000000的内存地址
  jmp $
GDT_START:
knull_dsc: dq 0
kcode_dsc: dq 0x00cf9a000000ffff ; a-e
kdata_dsc: dq 0x00cf92000000ffff
k16cd_dsc: dq 0x00009a000000ffff ; 16位代码段描述符
k16da_dsc: dq 0x000092000000ffff ; 16位数据段描述符
GDT_END:
GDT_PTR:
GDTLEN  dw GDT_END-GDT_START-1  ; GDT界限
GDTBASE  dd GDT_START

IDT_PTR:
IDTLEN  dw 0x3ff
IDTBAS  dd 0  ; 这是BIOS中断表的地址和长度，方便之后利用BIOS中断获取机器信息
```

## 调用BIOS中断

由于历史原因，现在高版本编译器编译C++生成的汇编不支持在16位模式了，我们之后收集机器信息、获取内存布局等都依赖BIOS提供的中断服务，但BIOS中断却需要在16位下调用，所以C语言环境下进行以下处理：

1. 保持C++上下文环境，即将保护模式下各种寄存器保存到内存1中。
2. 切换会16位实模式，调用BIOS中断，并将获取的信息保存到内存2中。
3. 切换会保护模式，加载内存1中数据到寄存器，即恢复上下文，读取内存2数据到C++数据结构中。

为实现以上功能，可以写一个汇编函数完成：

```asm
;; 实模式调用
realadr_call_entry:
  pushad     ; 保存通用寄存器
  push    ds
  push    es
  push    fs ; 保存4个段寄存器
  push    gs
  call save_eip_jmp ; 调用 save_eip_jmp 
  pop  gs
  pop  fs
  pop  es      ; 恢复4个段寄存器，栈是先进后出
  pop  ds
  popad       ; 恢复通用寄存器
  ret
save_eip_jmp:
  pop esi  ; 弹出call save_eip_jmp时保存的eip到esi寄存器中， 
  mov [PM32_EIP_OFF],esi ; 把eip保存到特定的内存空间中
  mov [PM32_ESP_OFF],esp ; 把esp保存到特定的内存空间中
  jmp dword far [cpmty_mode] ; 长跳转这里表示把 cpmty_mode 处的第一个4字节装入eip，把其后的2字节装入cs，即 0x18 0x1000 -> CS:EIP 
cpmty_mode:
  dd 0x1000
  dw 0x18
  jmp $
```

上面汇编中 `save_eip_jmp` 函数中将 0x18 段选择子，0x1000 是偏移地址，这里进行长跳转到此地址处，且为 16位地址，我们需要在此地址处放一段代码，用于进入实模式并调用BIOS中断，获取结果放入内存：

```asm
;; 16位模式
[bits 16]
_start:
_16_mode:
  mov  bp,0x20 ; 0x20是指向GDT中的16位数据段描述符 
  mov  ds, bp
  mov  es, bp
  mov  ss, bp
  mov  ebp, cr0
  and  ebp, 0xfffffffe
  mov  cr0, ebp ; CR0.P=0 关闭保护模式
  jmp  0:real_entry ; 刷新CS影子寄存器，真正进入实模式
real_entry:
  mov bp, cs
  mov ds, bp
  mov es, bp
  mov ss, bp ; 重新设置实模式下的段寄存器 都是CS中值，即为0 
  mov sp, 08000h ; 设置栈
  mov bp,func_table
  add bp,ax
  call [bp] ; 调用函数表中的汇编函数，ax是C函数中传递进来的
  cli
  call disable_nmi
  mov  ebp, cr0
  or  ebp, 1
  mov  cr0, ebp ; CR0.P=1 开启保护模式
  jmp dword 0x8 :_32bits_mode
[BITS 32]
_32bits_mode:
  mov bp, 0x10
  mov ds, bp
  mov ss, bp ; 重新设置保护模式下的段寄存器0x10是32位数据段描述符的索引
  mov esi,[PM32_EIP_OFF] ; 加载先前保存的EIP
  mov esp,[PM32_ESP_OFF] ; 加载先前保存的ESP
  jmp esi ; eip=esi 回到了realadr_call_entry函数中

func_table:  ; 函数表
  dw _getmmap ; 获取内存布局视图的函数
  dw _read ; 读取硬盘的函数
  dw _getvbemode ; 获取显卡VBE模式 
  dw _getvbeonemodeinfo ; 获取显卡VBE模式的数据
  dw _setvbemode ; 设置显卡VBE模式
```

之后将这段汇编代码编译为16位模式并通过之前的C++函数放入指定内存地址，随着汇编执行，到这里就进入实模式调用BIOS获取到了信息和内存视图。

## 二级引导器入口

二级引导器使用C++编写，入口函数中我们就可以进行相关数据收集了。

## 总结

